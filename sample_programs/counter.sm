//Synchronous Counter with 2 T-Flip-Flops and Display 

//	Prints a number to standard out based on
// 	 states of the TFFs
void DFA display()
{
	start
	{
		print0 	<- (state("clock") == "rising" && state("TFF1") == "high" && state("TFF2") == "high");	
		print1 	<- (state("clock") == "rising" && state("TFF1") == "start" && state("TFF2") == "start");
		print2 	<- (state("clock") == "rising" && state("TFF1") == "high" && state("TFF2") == "start");
		print3 	<- (state("clock") == "rising" && state("TFF1") == "start" && state("TFF2") == "high");
		start 	<- *;
	}

	print0
	{
		print("0\n");
		start 	<- *;
	}

	print1
	{
		print("1\n");
		start 	<- *;
	}

	print2
	{
		print("2\n");
		start 	<- *;
	}

	print3
	{
		print("3\n");
		start 	<- *;
	}	
}




// DFA to represent a clock
//  halfPeriod: integer to represent period/2 in ms
void DFA clock(int halfPeriod)
{
	// Start == low	
	// Wait halfPeriod ms, then toggle
	start
	{
		sleep(halfPeriod);
		rising		<- *;				
	}

	// state that triggers a toggle for the TFFs
	rising
	{		
		high  	<- *;
	}			
	
	high
	{
		sleep(halfPeriod);
		start 	<- *;
	}

	finish
	{
		return;
	}
}

// 1st T-FlipFlop in counter
//  Toggles on every rising clock
void DFA TFF1()
{
	// low output
	start
	{
		high 	<- (state("clock") == "rising");
		start 	<- *;
	}

	// high output
	high
	{
		start	<- (state("clock") == "rising");
		high 	<- *;
	}		
}

// 2nd T-FlipFlop in counter
//	Toggles on every clock only if TFF1 is high
void DFA TFF2()
{
	// low output
	start
	{
		high	<-(state("clock") == "rising" && state("TFF1") == "high");
		start 	<- *;
	}

	// high output
	high
	{
		start	<-(state("clock") == "rising" && state("TFF1") == "high");
		high 	<- *;
	}		
}


void DFA main()							
{
	int halfPeriod = 1000;

	start											
	{	
		concurrent(clock(halfPeriod), TFF1(), TFF2(), display());
		return;
	}	
}